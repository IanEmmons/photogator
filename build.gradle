plugins {
	id 'application'
	id 'eclipse'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'io.github.java-native:jssc:2.9.6'
}

group = 'org.virginiaso.photogator'
version = '1.0.4'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

application {
	mainClass = 'org.virginiaso.photogator.Photogator'
}

testing {
	suites {
		test {
			useJUnitJupiter('5.10.1')
		}
	}
}

jar {
	manifest {
		attributes(
			'Implementation-Title': 'Photogator',
			'Implementation-Vendor': 'Virginia Science Olympiad (VASO)',
			'Implementation-Version': project.version,
			'Main-Class': application.mainClass
		)
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
