plugins {
	id 'application'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'io.github.java-native:jssc:2.10.2'
	implementation 'org.slf4j:slf4j-api:2.0.17'
	implementation 'ch.qos.logback:logback-classic:1.5.21'
}

group = 'org.virginiaso.photogator'
version = '1.0.6'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

application {
	mainClass = 'org.virginiaso.photogator.Photogator'
}

testing {
	suites {
		test {
			useJUnitJupiter('6.0.1')	// org.junit.jupiter:junit-jupiter
		}
	}
}

jar {
	manifest {
		attributes(
			'Implementation-Title': 'Photogator',
			'Implementation-Vendor': 'Virginia Science Olympiad (VASO)',
			'Implementation-Version': project.version,
			'Main-Class': application.mainClass
		)
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
